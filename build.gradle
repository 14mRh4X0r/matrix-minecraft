plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'java'
}

group 'io.t2l'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDirs file('src/integration-test/resources'), file('src/integration-test/nodejs')
    }
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

configurations {
    shade
    compile.extendsFrom shade
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.bukkit:bukkit:1.13-R0.1-SNAPSHOT"
    shade "org.apache.httpcomponents:httpclient:4.5"
    shade "com.fasterxml.jackson.core:jackson-databind:2.9.7"
    shade "org.nanohttpd:nanohttpd:2.3.1"
    shade "org.nanohttpd:nanohttpd-nanolets:2.3.1"
    shade "com.github.javaplugs:minibus:0.4.10"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.awaitility:awaitility:3.1.5"
    integrationTestCompile sourceSets.main.output
    integrationTestCompile 'org.assertj:assertj-core:3.0.0'
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

task copyPlugin(type: Copy) {
    from "build/libs/matrix-minecraft-${version}.jar"
    rename { String filename -> return "MatrixMinecraft.jar" }
    into 'src/integration-test/resources/plugin'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
copyPlugin.dependsOn jar
integrationTest.dependsOn copyPlugin

integrationTest.mustRunAfter test